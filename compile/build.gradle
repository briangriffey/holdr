apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6

dependencies {
    compile 'net.sf.kxml:kxml2:2.3.0'
    compile 'com.sun.codemodel:codemodel:2.6'
    compile 'commons-io:commons-io:2.4'
    compile 'org.jetbrains:annotations:13.0'
    compile project(':compile-model')

    testCompile files("${System.properties['java.home']}/../lib/tools.jar")
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'com.google.testing.compile:compile-testing:0.6'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('sonatype.username') && project.hasProperty('sonatype.password')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { deployment -> signing.signPom(deployment) }

                def repoUrl
                if (project.version.endsWith("SNAPSHOT")) {
                    repoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                } else {
                    repoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                }

                repository(url: repoUrl) {
                    authentication(
                            userName: project.getProperty('sonatype.username'),
                            password: project.getProperty('sonatype.password'))
                }

                pom.project {
                    name 'Holdr (compile)'
                    packaging 'jar'
                    description 'Generates view holders from Android layout files'
                    url 'https://github.com/evant/holdr'

                    scm {
                        url 'git@github.com:evant/holdr.git'
                        connection 'scm:git:git@github.com:evant/holdr.git'
                        developerConnection 'scm:git:git@github.com:evant/holdr.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'evant'
                            name 'Evan Tatarka'
                        }
                    }
                }
            }
        }
    }
}
